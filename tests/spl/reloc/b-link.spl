/** B+ tree implementation of the link template for linearizable dictionaries. */

options "-abspreds -simplearrays -propreads"
  // TODO: -symbexec doesn't work anymore because function definitions have field reads inside!!
  // And normal mode doesn't work because things like lemma_int_valid_unfold take x as
  // parameter and then it thinks x is part of the footprint or something?
  // TODO try using the definition of the predicate intValid instead

include "flows.spl"
include "array_util.spl"


/** Flow and node domain definitions */

datatype FlowDom = fd(ks: Map<K, Int>, ir: Map<K, Int>)

/*
pure predicate domEq(p1: FlowDom, p2: FlowDom) {
  p1.ks == p2.ks && p1.ir == p2.ir
  //  (forall k: K :: p1.ks[k] == p2.ks[k]
  //   @(matching p1.ks[k] yields p2.ks[k]) @(matching p2.ks[k] yields p1.ks[k]))
  //    && (forall k: K :: p1.ir[k] == p2.ir[k]
  //     @(matching p1.ir[k] yields p2.ir[k]) @(matching p2.ir[k] yields p1.ir[k]))

}
 */

pure predicate domLeq(p1: FlowDom, p2: FlowDom) {
  (forall k: K :: p1.ks[k] <= p2.ks[k])
    //@(matching p1.ks[k] yields p2.ks[k]) @(matching p2.ks[k] yields p1.ks[k]))
    && (forall k: K :: p1.ir[k] <= p2.ir[k])
    //@(matching p1.ir[k] yields p2.ir[k]) @(matching p2.ir[k] yields p1.ir[k]))
}

pure function domPlus(p1: FlowDom, p2: FlowDom) returns (p: FlowDom)
  ensures forall k: K :: p.ks[k] == p1.ks[k] + p2.ks[k]
  ensures forall k: K :: p.ir[k] == p1.ir[k] + p2.ir[k]

pure function domMult(p1: FlowDom, p2: FlowDom) returns (p: FlowDom)
  ensures forall k: K :: p.ks[k] == p1.ks[k] * p2.ks[k]
  ensures forall k: K :: p.ir[k] == p1.ir[k] * p2.ir[k]

pure function domZero() returns(p: FlowDom)
  ensures forall k: K :: p.ks[k] == 0 && p.ir[k] == 0


datatype InrFn = inrfnC(inrfn: Map<Node, Set<K>>, inrdom: Set<Node>) | inrfnUndef;
  
datatype NodeDom = nd(contents: Set<K>, inr: InrFn);

pure predicate ndLeq(a1: NodeDom, a2: NodeDom) {
  (forall k: K :: k in a1.contents ==> k in a2.contents)
    && (a2.inr == inrfnUndef || a1.inr != inrfnUndef && a2.inr != inrfnUndef
       && (forall x: Node :: x in a1.inr.inrdom ==> x in a2.inr.inrdom && a1.inr.inrfn[x] == a2.inr.inrfn[x]))
}

define emptyInt(x, Ix) {
  Ix.FP == {x} && domEq(Ix.inf[x], domZero()) == true && Ix.FPo == {}
  && Ix.na == nd({}, inrfnC({n: Node :: {}}, {x}))
}


/** Definition of a node */

struct Node {
  var len: Int;
  var keys: Array<K>;
  var ptrs: Array<Node>;
  var next: Node;
  ghost var isFirst: Bool;
  ghost var rangeLb: K;
  ghost var rangeUb: K;
}

define unchanged(x) {
  x.len == old(x.len) && x.next == old(x.next)
    && x.rangeLb == old(x.rangeLb) && x.rangeUb == old(x.rangeUb)
    && x.keys == old(x.keys) && x.ptrs == old(x.ptrs)
    && x.keys.length == old(x.keys.length) && x.keys.map == old(x.keys.map)
    && x.ptrs.length == old(x.ptrs.length) && x.ptrs.map == old(x.ptrs.map)
}


/** Definition of heap representation, hrep */

var B: Int
axiom B > 1

define hrepSpatial(x) {
  acc(x) &*& acc(x.keys) &*& acc(x.ptrs)
}


function fm_child(x: Node, i: Int) returns (f: FlowDom)
  requires 0 <= i <= x.len
{
  fd({ k : K ::
      i < x.len ?
        ((i == 0 || le(x.keys[i-1], k)) && lt(k, x.keys[i]) ? 1 : 0)
        : (i == x.len && x.next == null ?
            ((i == 0 || le(x.keys[i-1], k)) ? 1 : 0)
            : (i == x.len && x.next != null ?
                ((i == 0 || le(x.keys[i-1], k)) && lt(k, x.keys[i]) ? 1 : 0)
                : 0)) },
      { k : K :: 0 } )
}

function fm_next(x: Node, inff: Map<Node, FlowDom>) returns (f: FlowDom)
{
  fd({ k: K :: x.next == null ? 0 : (le(x.keys[x.len], k) ? 1 : 0) },
     { k: K :: inff[x].ir[k] >= 1 && inff[x].ks[k] <= 0 && x.next != null && le(x.keys[x.len], k) ? 1 : 0 })
}


function fm_ind(x: Node, inff: Map<Node, FlowDom>, lenn: Int, i: Int)
  returns (f: Map<Node, Map<Node, FlowDom>>)
{
  { n: Node :: { n1: Node ::
    n != x ? domZero()
    : (i < 0 ? (n1 == x.next ? fm_next(x, inff) : domZero())
       : (i <= x.len && {n1} == {x.ptrs[i]} ? fm_child(x, i)
          : fm_ind(x, inff, lenn, i - 1)[n][n1]))
   }
  }
}

function FPo_ind(x: Node, lenn: Int, i: Int) returns (X: Set<Node>)
{
  i < 0 ? (x.next != null ? {x.next} : {})
    : (i <= x.len ? {x.ptrs[i]} ++ FPo_ind(x, lenn, i - 1)
       : FPo_ind(x, lenn, i - 1))
}

function inflow(x: Node) returns (i: Map<Node, FlowDom>)
{
  { n: Node :: n != x ? domZero()
      : fd({k: K :: (x.isFirst || le(x.rangeLb, k)) && (x.next == null || lt(k, x.rangeUb)) ? 1 : 0},
           {k: K :: (x.isFirst || le(x.rangeLb, k)) ? 1 : 0}) }
}

function nodeabs(x: Node, inff: Map<Node, FlowDom>, C: Set<K>) returns (a: NodeDom)
{
  nd(C, inrfnC({n: Node :: n != x ? {} : {k: K :: inff[x].ks[k] >= 1 || inff[x].ir[k] >= 1}}, {x}))
}

function int_ind(x: Node, lenn: Int, i: Int) returns (I: Interface)
{
  int(inflow(x), nodeabs(x, inflow(x), {}), fm_ind(x, inflow(x), lenn, i), {x}, FPo_ind(x, x.len, i))
}

define hrep(x, I) {
  hrepSpatial(x) &*& I.FP == {x} &*& x.next != x
    && 0 <= x.len && x.len < 2*B && x.keys.length == 2*B && x.ptrs.length == 2*B
    // Keys are sorted
    && (forall i: Int, j: Int :: 0 <= i < j < x.len ==> lt(x.keys[i], x.keys[j]))
    // Keys are in the range
    && (forall i: Int :: 0 <= i < x.len && !x.isFirst ==> le(x.rangeLb, x.keys[i]))
    && (forall i: Int :: 0 <= i < x.len && x.next != null ==> lt(x.keys[i], x.rangeUb))
    && (x.ptrs[0] == null  // Leaf
       && I.inf == inflow(x)
       && I.na.contents == set_of_map(x.keys.map, 0, x.len)
       && (x.next == null && I.FPo == {}
          || x.next != null && I.FPo == {x.next}
          && (forall k: K :: le(x.keys[x.len], k) ==> I.fm[x][x.next].ks[k] == 1)
          && (forall k: K :: lt(k, x.keys[x.len]) ==> I.fm[x][x.next].ks[k] == 0
          @(matching x.next yields I.fm[x][x.next].ks[k])))
     || x.ptrs[0] != null  // Internal
       && I == int_ind(x, x.len, x.len)
       && (forall i: Int :: 0 <= i <= x.len ==> x != x.ptrs[i] && x.ptrs[i] != x.next)
       && (forall i: Int, j: Int :: 0 <= i < j <= x.len ==> x.ptrs[i] != x.ptrs[j])
       && (forall i: Int :: 0 <= i <= x.len ==> x.ptrs[i] != null)
      )
}

lemma lemma_fm(x: Node, Ix: Interface, k: K, i: Int, j: Int)
  requires hrep(x, Ix) &*& x.ptrs[0] != null
  ensures hrepSpatial(x) &*& unchanged(x)
  ensures x.next != null && le(x.keys[x.len], k)
    ==> fm_ind(x, Ix.inf, x.len, j)[x][x.next].ks[k] >= 1
  ensures i <= j && 0 <= i < x.len && (i == 0 || le(x.keys[i-1], k)) && lt(k, x.keys[i])
    ==> fm_ind(x, Ix.inf, x.len, j)[x][x.ptrs[i]].ks[k] >= 1
  ensures i <= j && i == x.len && x.next == null && (i == 0 || le(x.keys[i-1], k))
    ==> fm_ind(x, Ix.inf, x.len, j)[x][x.ptrs[i]].ks[k] >= 1
  ensures i <= j && i == x.len && x.next != null && (i == 0 || le(x.keys[i-1], k)) && lt(k, x.keys[i])
    ==> fm_ind(x, Ix.inf, x.len, j)[x][x.ptrs[i]].ks[k] >= 1
{
  if (j >= 0) {
    lemma_fm(x, Ix, k, i, j - 1);
  }
}


// TODO probably define this inductively too
// TODO findNext's spec should use this. Also, make sure you can prove whatever Coq relies on this.
pure function outset(x: Node, flmp: Map<Node, Map<Node, FlowDom>>) returns (O: Set<K>)
  //  ensures forall k: K :: k in O ==> (exists y: Node :: flmp[x][y].ks[k] > 0 @(matching O yields flmp[x][y].ks[k]))
  //{ { k: K :: exists y: Node :: flmp[x][y].ks[k] > 0 } }  // TODO why not? (see hashtbl.findNext)

// The good condition
define nu(x, I) {
    // Contents subsetof keyset
    (forall k: K :: k in I.na.contents ==> I.inf[x].ks[k] >= 1)
    && (forall k: K, y: Node :: k !in I.na.contents || I.fm[x][y].ks[k] == 0)
    // Edgesets are disjoint
    && (forall k: K, y: Node, z: Node :: y == z
       || I.fm[x][y].ks[k] == 0 || I.fm[x][z].ks[k] == 0)
    // Inreach keyset subsetof inset
    && (forall k: K :: I.inf[x].ir[k] >= 1 && k !in outset(x, I.fm) ==> I.inf[x].ks[k] >= 1)
    // Inreach edgesets subsetof normal edgesets
    && (forall k: K, y: Node :: I.fm[x][y].ir[k] <= I.fm[x][y].ks[k])
    // If k âˆˆ inreach \ inset, then inreach edgeset is same as normal edgeset (for step)
    && (forall k: K, y: Node :: I.inf[x].ir[k] >= 1 && I.inf[x].ks[k] <= 0
       && I.fm[x][y].ks[k] >= 1 ==> I.fm[x][y].ir[k] >= 1)
    // The node label stores the actual inreach
    && I.na.inr.inrdom == {x}
    && (forall k: K :: (k in I.na.inr.inrfn[x]) ==> (I.inf[x].ks[k] >= 1 || I.inf[x].ir[k] >= 1))
    && (forall k: K :: (I.inf[x].ks[k] >= 1 || I.inf[x].ir[k] >= 1) ==> (k in I.na.inr.inrfn[x]))
}


lemma keyset_implies_leaf(x: Node, Ix: Interface, k: K, i: Int)
  requires hrep(x, Ix) &*& intValid(Ix) == true
  requires Ix.inf[x].ks[k] == 1 && (forall y: Node :: Ix.fm[x][y].ks[k] == 0)
  requires (i <= 0 || le(x.keys[i-1], k)) && 0 <= i <= x.len
  ensures hrep(x, Ix) &*& unchanged(x)
  ensures x.ptrs[0] == null  // x is a leaf
{
  if (x.ptrs[0] != null) {
    lemma_fm(x, Ix, k, i, x.len);
    if (i < x.len && !lt(k, x.keys[i])) {
      keyset_implies_leaf(x, Ix, k, i + 1);
    }
    pure assert false; // TODO why is this necessary?!
  }
}

lemma compute_int(x: Node) returns (I: Interface)
  requires hrepSpatial(x)
  requires x.next != x
    && 0 <= x.len && x.len < 2*B && x.keys.length == 2*B && x.ptrs.length == 2*B
    && (forall i: Int, j: Int :: 0 <= i < j < x.len ==> lt(x.keys[i], x.keys[j]))
    && (forall i: Int :: 0 <= i < x.len && !x.isFirst ==> le(x.rangeLb, x.keys[i]))
    && (forall i: Int :: 0 <= i < x.len && x.next != null ==> lt(x.keys[i], x.rangeUb))
    && (x.ptrs[0] != null ==>  // Internal
       (forall i: Int :: 0 <= i <= x.len ==> x != x.ptrs[i] && x.ptrs[i] != x.next)
       && (forall i: Int, j: Int :: 0 <= i < j <= x.len ==> x.ptrs[i] != x.ptrs[j])
       && (forall i: Int :: 0 <= i <= x.len ==> x.ptrs[i] != null))
  ensures hrep(x, I) &*& unchanged(x)
  ensures intValid(I) == true
{
  if (x.ptrs[0] == null) {
    // TODO by induction, from keys sorted
    pure assume forall k in set_of_map(x.keys.map, 0, x.len) :: le(x.keys[0], k) && lt(k, x.keys[x.len]);
    // TODO by induction, from keys < x.rangeUb
    pure assume forall k in set_of_map(x.keys.map, 0, x.len) :: x.next == null || lt(k, x.rangeUb);
    var xfpo: Set<Node>;
    var xfm := { n: Node :: { n1: Node :: domZero() } };
    if (x.next == null) {
      xfpo := {};
      I := int(inflow(x), nodeabs(x, inflow(x), set_of_map(x.keys.map, 0, x.len)), xfm, {x}, xfpo);

      // TODO define outset inductively, and prove this from that definition
      pure assume (forall k: K :: I.inf[x].ir[k] >= 1 && k !in outset(x, I.fm) ==> I.inf[x].ks[k] >= 1);

      //  lemma_int_valid_fold(x, I);
      pure assert I != intUndef
        && (forall n: Node, n1: Node ::
           (n !in I.FP ==> domEq(I.inf[n], domZero()) && domEq(I.fm[n][n1], domZero()))
           && (n1 !in I.FPo ==> domEq(I.fm[n][n1], domZero())))
        && (forall n: Node :: n !in I.FP || n !in I.FPo)
        && (forall n: Node :: domLeq(domZero(), I.inf[n]))
        && (forall n: Node, n1: Node :: domLeq(domZero(), I.fm[n][n1]))
        && nu(x, I);
      pure assume intValid(I) == true;
      return I;
    } else {
      var f := fd({ k: K :: le(x.keys[x.len], k) ? 1 : 0 },
                  { k: K :: le(x.keys[x.len], k) ? 1 : 0 });
      xfm := xfm[x := xfm[x][x.next := f]];
      xfpo := {x.next};
      I := int(inflow(x), nodeabs(x, inflow(x), set_of_map(x.keys.map, 0, x.len)), xfm, {x}, xfpo);

      // TODO define outset inductively, and prove this from that definition
      pure assume (forall k: K :: I.inf[x].ir[k] >= 1 && k !in outset(x, I.fm) ==> I.inf[x].ks[k] >= 1);

      //  lemma_int_valid_fold(x, I);
      pure assert I != intUndef
        && (forall n: Node, n1: Node ::
           (n !in I.FP ==> domEq(I.inf[n], domZero()) && domEq(I.fm[n][n1], domZero()))
           && (n1 !in I.FPo ==> domEq(I.fm[n][n1], domZero())))
        && (forall n: Node :: n !in I.FP || n !in I.FPo)
        && (forall n: Node :: domLeq(domZero(), I.inf[n]))
        && (forall n: Node, n1: Node :: domLeq(domZero(), I.fm[n][n1]))
        && nu(x, I);
      pure assume intValid(I) == true;
      return I;
    }
  } else {
    I := int_ind(x, x.len, x.len);
    lemma_int_props(x, x.len);

    // TODO define outset inductively, and prove this from that definition
    pure assume (forall k: K :: I.inf[x].ir[k] >= 1 && k !in outset(x, I.fm) ==> I.inf[x].ks[k] >= 1);

    //  lemma_int_valid_fold(x, I);
    pure assert I != intUndef
    && (forall n: Node, n1: Node ::
       (n !in I.FP ==> domEq(I.inf[n], domZero()) && domEq(I.fm[n][n1], domZero()))
       && (n1 !in I.FPo ==> domEq(I.fm[n][n1], domZero())))
    && (forall n: Node :: n !in I.FP || n !in I.FPo)
    && (forall n: Node :: domLeq(domZero(), I.inf[n]))
    && (forall n: Node, n1: Node :: domLeq(domZero(), I.fm[n][n1]))
    && nu(x, I);
    pure assume intValid(I) == true;
    return I;
  }
}

// TODO by induction
lemma lemma_int_props(x: Node, i: Int)
  requires hrepSpatial(x)
  requires (forall i: Int :: 0 <= i <= x.len ==> x != x.ptrs[i]) && x.next != x

  ensures hrepSpatial(x) &*& unchanged(x)
  ensures (forall k: K, y: Node :: fm_ind(x, inflow(x), x.len, i)[x][y].ir[k]
    <= fm_ind(x, inflow(x), x.len, i)[x][y].ks[k])
  ensures (forall k: K, y: Node, z: Node :: y == z
    || fm_ind(x, inflow(x), x.len, i)[x][y].ks[k] == 0 || fm_ind(x, inflow(x), x.len, i)[x][z].ks[k] == 0)
  ensures (forall k: K, y: Node :: inflow(x)[x].ir[k] >= 1 && inflow(x)[x].ks[k] <= 0
     && fm_ind(x, inflow(x), x.len, i)[x][y].ks[k] >= 1 ==> fm_ind(x, inflow(x), x.len, i)[x][y].ir[k] >= 1)
  ensures forall n: Node, n1: Node :: domLeq(domZero(), fm_ind(x, inflow(x), x.len, i)[n][n1])
  ensures forall n: Node, n1: Node ::
    (n !in {x} ==> domEq(fm_ind(x, inflow(x), x.len, i)[n][n1], domZero()))
    && (n1 !in FPo_ind(x, x.len, i) ==> domEq(fm_ind(x, inflow(x), x.len, i)[n][n1], domZero()))
  ensures forall n: Node :: n !in {x} || n !in FPo_ind(x, x.len, i)

// TODO by induction
lemma lemma_int_stable(x: Node, i: Int)
  requires hrepSpatial(x)
  ensures hrepSpatial(x) &*& unchanged(x)
  ensures i > x.len ==> int_ind(x, x.len, i) == int_ind(x, x.len, x.len)


/** Begin programs */

/** Initialize root note */
procedure init() returns (x: Node, I: Interface)
  ensures hrep(x, I)
{
  x := new Node;
  x.keys := new Array<K>(2*B);
  x.ptrs := new Array<Node>(2*B);
  arr_init_node(x.ptrs, 2*B, null);
  x.len := 0;
  x.next := null;
  
  var xkc := { k: K :: le(bottom, k) && lt(k, top) ? 1 : 0 };
  var xir := { k: K :: le(bottom, k) && lt(k, top) ? 1 : 0 }; // TODO, really?
  var xfm := { n: Node :: { n1: Node :: domZero } };
  var xinf := { n: Node :: n == x ? fd(xkc, xir) : domZero };
  var inrSet: Set<K> := { k: K :: xinf[x].ks[k] >= 1 || xinf[x].ir[k] >= 1 };
  var i := inrfnC({ n: Node :: n == x ? inrSet : {} }, {x});
  I := int(xinf, nd({}, i), xfm, {x}, {});
}

/** findNext */
procedure findNext(x: Node, k: K, Ix: Interface)
  returns (n: Node)
  requires hrep(x, Ix) &*& intValid(Ix) == true
  ensures hrep(x, Ix)
  ensures n != null ==> Ix.fm[x][n].ks[k] >= 1
  ensures n == null ==> (forall y: Node :: Ix.fm[x][y].ks[k] == 0)
{
  if (x.ptrs[0] == null) {  // Leaf node
    if (x.next == null) {
      lemma_int_valid_unfold(x, Ix);
      return null;
    }
    if (le(x.keys[x.len], k)) {
      return x.next;
    } else {
      lemma_int_valid_unfold(x, Ix);
      var y: Node;
      if (y in Ix.FPo) {
        pure assert Ix.fm[x][y].ks[k] == 0;
      } else {
        pure assert Ix.fm[x][y].ks[k] == 0;
      }
      pure assume forall y: Node :: Ix.fm[x][y].ks[k] == 0;
      return null;
    }
  }

  var found, i := arr_find(x.keys, x.len, k);

  if (i < x.len && k == x.keys[i]) {  // arr_find finds upper bound, we want lower
    i := i + 1;
  }

  lemma_fm(x, Ix, k, i, x.len);
  if (i == x.len && x.next != null) {
    if (lt(k, x.keys[i]))
      n := x.ptrs[i];
    else
      n := x.next;
  } else {
    pure assert i <= 0 || le(x.keys[i-1], k);
    n := x.ptrs[i];
  }
  // return n;
}


/** member */
procedure member(x: Node, k: K, Ix: Interface)
  returns (succ: Bool, res: Bool, Ix1: Interface)
  requires hrep(x, Ix) &*& intValid(Ix) == true
  requires Ix.inf[x].ks[k] == 1 && (forall y: Node :: Ix.fm[x][y].ks[k] == 0)
  ensures hrep(x, Ix)
  ensures intEq(Ix, Ix1) == true
  // decisiveOpSpec(x, Ix1, res, k):
  ensures succ ==> (Ix1.na.contents == Ix.na.contents && (res == k in Ix.na.contents))
{
  keyset_implies_leaf(x, Ix, k, 0);

  var idx: Int;
  res, idx := arr_find(x.keys, x.len, k);

  return true, res, Ix;
}


/** insert */
procedure insert(x: Node, k: K, Ix: Interface)
  returns (succ: Bool, res: Bool, Ix1: Interface)
  requires hrep(x, Ix) &*& intValid(Ix) == true
  requires Ix.inf[x].ks[k] == 1 && (forall y: Node :: Ix.fm[x][y].ks[k] == 0)
  ensures hrep(x, Ix1)
  ensures intEq(Ix, Ix1) == true
  // decisiveOpSpec(x, Ix1, res, k):
  ensures succ ==> (Ix1.na.contents == Ix.na.contents ++ {k} && (res == k !in Ix.na.contents))
{
  keyset_implies_leaf(x, Ix, k, 0);

  if (x.len < 2*B - 1) {

    ghost var m0 := x.keys.map;
    x.keys[x.len + 1] := x.keys[x.len];
    set_of_map_equal(m0, x.keys.map, 0, 0, x.len);
    ghost var m := x.keys.map; var old_len := x.len;
    var idx, new_len := arr_insert(x.keys, k, x.len);
    map_insert_content_set(m, x.keys.map, idx, k, x.len, new_len);
    x.len := new_len;

    // Define Ix1 to be Ix, but with k added to contents
    Ix1 := copy_interface(Ix, nd(Ix.na.contents ++ {k}, Ix.na.inr));
    lemma_int_valid_unfold(x, Ix);
    lemma_int_valid_fold(x, Ix1);

    // TODO the problem with this is again that symbexec can't handle it when function bodies
    // access the heap. Try standard backend.
    return true, k !in Ix.na.contents, Ix1;
  } else {
    return false, false, Ix;
  }
}


/** delete */
procedure delete(x: Node, k: K, Ix: Interface)
  returns (succ: Bool, res: Bool, Ix1: Interface)
  requires hrep(x, Ix) &*& intValid(Ix) == true
  requires Ix.inf[x].ks[k] == 1 && (forall y: Node :: Ix.fm[x][y].ks[k] == 0)
  ensures hrep(x, Ix1)
  ensures intEq(Ix, Ix1) == true
  // decisiveOpSpec(x, Ix1, res, k):
  ensures succ ==> (Ix1.na.contents == Ix.na.contents -- {k} && (res == k in Ix.na.contents))
{
  keyset_implies_leaf(x, Ix, k, 0);

  ghost var m := x.keys.map;

  var new_len, idx := arr_delete(x.keys, k, x.len);

  map_delete_content_set(m, x.keys.map, x.len, new_len, idx, k);

  ghost var m1 := x.keys.map;
  x.keys[new_len] := x.keys[x.len];
  set_of_map_equal(m1, x.keys.map, 0, 0, new_len);

  x.len := new_len;

  // Define Ix1 to be Ix, but with k removed from contents
  Ix1 := copy_interface(Ix, nd(Ix.na.contents -- {k}, Ix.na.inr));
  lemma_int_valid_unfold(x, Ix);
  lemma_int_valid_fold(x, Ix1);

  return true, (k in Ix.na.contents), Ix1;
}

