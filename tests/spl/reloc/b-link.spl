/** B+ tree implementation of the link template for linearizable dictionaries. */

options "-abspreds -simplearrays -propreads -symbexec"

include "flows.spl"
include "array_util.spl"


/** Flow and node domain definitions */

datatype FlowDom = fd(ks: Map<K, Int>, ir: Map<K, Int>)

/*
pure predicate domEq(p1: FlowDom, p2: FlowDom) {
  p1.ks == p2.ks && p1.ir == p2.ir
  //  (forall k: K :: p1.ks[k] == p2.ks[k]
  //   @(matching p1.ks[k] yields p2.ks[k]) @(matching p2.ks[k] yields p1.ks[k]))
  //    && (forall k: K :: p1.ir[k] == p2.ir[k]
  //     @(matching p1.ir[k] yields p2.ir[k]) @(matching p2.ir[k] yields p1.ir[k]))

}
 */

pure predicate domLeq(p1: FlowDom, p2: FlowDom) {
  (forall k: K :: p1.ks[k] <= p2.ks[k])
    //@(matching p1.ks[k] yields p2.ks[k]) @(matching p2.ks[k] yields p1.ks[k]))
    && (forall k: K :: p1.ir[k] <= p2.ir[k])
    //@(matching p1.ir[k] yields p2.ir[k]) @(matching p2.ir[k] yields p1.ir[k]))
}

pure function domPlus(p1: FlowDom, p2: FlowDom) returns (p: FlowDom)
  ensures forall k: K :: p.ks[k] == p1.ks[k] + p2.ks[k]
  ensures forall k: K :: p.ir[k] == p1.ir[k] + p2.ir[k]

pure function domMult(p1: FlowDom, p2: FlowDom) returns (p: FlowDom)
  ensures forall k: K :: p.ks[k] == p1.ks[k] * p2.ks[k]
  ensures forall k: K :: p.ir[k] == p1.ir[k] * p2.ir[k]

pure function domZero() returns(p: FlowDom)
  ensures forall k: K :: p.ks[k] == 0 && p.ir[k] == 0


datatype InrFn = inrfnC(inrfn: Map<Node, Set<K>>, inrdom: Set<Node>) | inrfnUndef;
  
datatype NodeDom = nd(contents: Set<K>, inr: InrFn);

pure predicate ndLeq(a1: NodeDom, a2: NodeDom) {
  (forall k: K :: k in a1.contents ==> k in a2.contents)
    && (a2.inr == inrfnUndef || a1.inr != inrfnUndef && a2.inr != inrfnUndef
       && (forall x: Node :: x in a1.inr.inrdom ==> x in a2.inr.inrdom && a1.inr.inrfn[x] == a2.inr.inrfn[x]))
}


/** Definition of a node */

struct Node {
  var len: Int;
  var keys: Array<K>;
  var ptrs: Array<Node>;
  var next: Node;
}

define unchanged(x) {
  x.len == old(x.len) && x.next == old(x.next)
    && x.keys == old(x.keys) && x.ptrs == old(x.ptrs)
    && x.keys.length == old(x.keys.length) && x.keys.map == old(x.keys.map)
    && x.ptrs.length == old(x.ptrs.length) && x.ptrs.map == old(x.ptrs.map)
}


/** Definition of heap representation, hrep */

var B: Int
axiom B > 1

define hrepSpatial(x) {
  acc(x) &*& acc(x.keys) &*& acc(x.ptrs)
}


function fm_child(x: Node, i: Int) returns (f: FlowDom)
  requires 0 <= i <= x.len
{
  fd({ k : K ::
      i < x.len ?
        ((i == 0 || le(x.keys[i-1], k)) && lt(k, x.keys[i]) ? 1 : 0)
        : (i == x.len && x.next == null ?
            ((i == 0 || le(x.keys[i-1], k)) ? 1 : 0)
            : (i == x.len && x.next != null ?
                ((i == 0 || le(x.keys[i-1], k)) && lt(k, x.keys[i]) ? 1 : 0)
                : 0)) },
      { k : K :: 0 } )
}

function fm_next(x: Node, inff: Map<Node, FlowDom>) returns (f: FlowDom)
{
  fd({ k: K :: x.next == null ? 0 : (le(x.keys[x.len], k) ? 1 : 0) },
    {k: K :: inff[x].ir[k] >= 1 && inff[x].ks[k] <= 0 && x.next != null && le(x.keys[x.len], k) ? 1 : 0 })
}


function fm_ind(x: Node, inff: Map<Node, FlowDom>, i: Int)
  returns (f: Map<Node, Map<Node, FlowDom>>)
{
  { n: Node :: { n1: Node ::
    i < 0 ? domZero()
    : (n != x ? domZero()
       : (i <= x.len && {n1} == {x.ptrs[i]} ? fm_child(x, i)
          : (i == x.len + 1 && n1 == x.next ? fm_next(x, inff)
             : fm_ind(x, inff, i - 1)[n][n1])))
   }
        @(matching f[n] yields fm_child(x, i))
        @(matching f[n] yields fm_next(x, inff))
          @(matching f[n] yields fm_ind(x, inff, i - 1)[n])
  }
}

function FPo_ind(x: Node, i: Int) returns (X: Set<Node>)
{
  i < 0 ? {}
    : (i <= x.len ? {x.ptrs[i]} ++ FPo_ind(x, i - 1)
       : (i == x.len + 1 && x.next != null ? {x.next} ++ FPo_ind(x, i - 1)
          : FPo_ind(x, i - 1)))
}

define hrep(x, I) {
  hrepSpatial(x) &*& I.FP == {x} &*& x.next != x
    && 0 <= x.len && x.len < 2*B && x.keys.length == 2*B && x.ptrs.length == 2*B
    // Keys are sorted
    && (forall i: Int, j: Int :: 0 <= i < j < x.len ==> lt(x.keys[i], x.keys[j]))
    && (x.ptrs[0] == null  // Leaf
       && I.na.contents == set_of_map(x.keys.map, 0, x.len)
       && (x.next == null && I.FPo == {}
          || x.next != null && I.FPo == {x.next}
          && (forall k: K :: le(x.keys[x.len], k) ==> I.fm[x][x.next].ks[k] == 1)
          && (forall k: K :: lt(k, x.keys[x.len]) ==> I.fm[x][x.next].ks[k] == 0
          @(matching x.next yields I.fm[x][x.next].ks[k])))
     || x.ptrs[0] != null  // Internal
       && I.na.contents == {}
       && (forall i: Int :: 0 <= i <= x.len ==> x != x.ptrs[i])
       && (forall i: Int, j: Int :: 0 <= i < j <= x.len ==> x.ptrs[i] != x.ptrs[j])
       && (forall i: Int, k: K :: 0 <= i <= x.len ==> x.ptrs[i] != null && x.ptrs[i] in I.FPo)
       && I.fm == fm_ind(x, I.inf, x.len + 1)
       && I.FPo == FPo_ind(x, x.len + 1)
      )
}

lemma lemma_fm(x: Node, Ix: Interface, k: K, i: Int, j: Int)
  requires hrep(x, Ix) &*& x.ptrs[0] != null
  ensures hrepSpatial(x) &*& unchanged(x)
  ensures i <= j && 0 <= i < x.len && (i == 0 || le(x.keys[i-1], k)) && lt(k, x.keys[i])
    ==> fm_ind(x, Ix.inf, j)[x][x.ptrs[i]].ks[k] >= 1
  ensures i <= j && i == x.len && x.next == null && (i == 0 || le(x.keys[i-1], k))
    ==> fm_ind(x, Ix.inf, j)[x][x.ptrs[i]].ks[k] >= 1
  ensures i <= j && i == x.len && x.next != null && (i == 0 || le(x.keys[i-1], k)) && lt(k, x.keys[i])
    ==> fm_ind(x, Ix.inf, j)[x][x.ptrs[i]].ks[k] >= 1
  ensures x.len + 1 <= j && x.next != null && le(x.keys[x.len], k)
    ==> fm_ind(x, Ix.inf, j)[x][x.next].ks[k] >= 1
{
  if (j >= 0) {
    lemma_fm(x, Ix, k, i, j - 1);
  }
}


// TODO probably define this inductively too
pure function outset(x: Node, flmp: Map<Node, Map<Node, FlowDom>>) returns (O: Set<K>)
  ensures forall k: K :: k in O ==> (exists y: Node :: flmp[x][y].ks[k] > 0 @(matching O yields flmp[x][y].ks[k]))
  //{ { k: K :: exists y: Node :: flmp[x][y].ks[k] > 0 } }  // TODO why not? (see hashtbl.findNext)

// The good condition
define nu(x, I) {
    // Contents subsetof keyset
    (forall k: K :: k in I.na.contents ==> I.inf[x].ks[k] >= 1)
    && (forall k: K, y: Node :: k !in I.na.contents || I.fm[x][y].ks[k] == 0)
    // Edgesets are disjoint
    && (forall k: K, y: Node, z: Node :: y == z
       || I.fm[x][y].ks[k] == 0 || I.fm[x][z].ks[k] == 0)
    // Inreach keyset subsetof inset
    && (forall k: K :: I.inf[x].ir[k] >= 1 && k !in outset(x, I.fm) ==> I.inf[x].ks[k] >= 1)
    // Inreach edgesets subsetof normal edgesets
    && (forall k: K, y: Node :: I.fm[x][y].ir[k] <= I.fm[x][y].ks[k])
    // If k âˆˆ inreach \ inset, then inreach edgeset is same as normal edgeset (for step)
    && (forall k: K, y: Node :: I.inf[x].ir[k] >= 1 && I.inf[x].ks[k] <= 0
       && I.fm[x][y].ks[k] >= 1 ==> I.fm[x][y].ir[k] >= 1)
    // The node label stores the actual inreach
    && I.na.inr.inrdom == {x}
    && (forall k: K :: (k in I.na.inr.inrfn[x]) ==> (I.inf[x].ks[k] >= 1 || I.inf[x].ir[k] >= 1))
    && (forall k: K :: (I.inf[x].ks[k] >= 1 || I.inf[x].ir[k] >= 1) ==> (k in I.na.inr.inrfn[x]))
}


lemma keyset_implies_leaf(x: Node, Ix: Interface, k: K, i: Int)
  requires hrep(x, Ix) &*& intValid(Ix) == true
  requires Ix.inf[x].ks[k] == 1 && (forall y: Node :: Ix.fm[x][y].ks[k] == 0)
  requires (i <= 0 || le(x.keys[i-1], k)) && 0 <= i <= x.len
  ensures hrep(x, Ix) &*& unchanged(x)
  ensures x.ptrs[0] == null  // x is a leaf
{
  if (x.ptrs[0] != null) {
    lemma_fm(x, Ix, k, i, x.len + 1);
    if (i < x.len && !lt(k, x.keys[i])) {
      keyset_implies_leaf(x, Ix, k, i + 1);
      pure assume false; // TODO
    }
  }
}

/** Begin programs */

/** Initialize root note */
procedure init() returns (x: Node, I: Interface)
  ensures hrep(x, I)
{
  x := new Node;
  x.keys := new Array<K>(2*B);
  x.ptrs := new Array<Node>(2*B);
  arr_init_node(x.ptrs, 2*B, null);
  x.len := 0;
  x.next := null;
  
  var xkc := { k: K :: le(bottom, k) && lt(k, top) ? 1 : 0 };
  var xir := { k: K :: le(bottom, k) && lt(k, top) ? 1 : 0 }; // TODO, really?
  var xfm := { n: Node :: { n1: Node :: domZero } };
  var xinf := { n: Node :: n == x ? fd(xkc, xir) : domZero };
  var inrSet: Set<K> := { k: K :: xinf[x].ks[k] >= 1 || xinf[x].ir[k] >= 1 };
  var i := inrfnC({ n: Node :: n == x ? inrSet : {} }, {x});
  I := int(xinf, nd({}, i), xfm, {x}, {});
}

/** findNext */
procedure findNext(x: Node, k: K, Ix: Interface)
  returns (n: Node)
  requires hrep(x, Ix) &*& intValid(Ix) == true
  ensures hrep(x, Ix)
  ensures n != null ==> Ix.fm[x][n].ks[k] >= 1
  ensures n == null ==> (forall y: Node :: Ix.fm[x][y].ks[k] == 0)
{
  if (x.ptrs[0] == null) {  // Leaf node
    if (x.next == null) {
      lemma_int_valid_unfold(x, Ix);
      return null;
    }
    if (le(x.keys[x.len], k)) {
      return x.next;
    } else {
      lemma_int_valid_unfold(x, Ix);
      var y: Node;
      if (y in Ix.FPo) {
        pure assert Ix.fm[x][y].ks[k] == 0;
      } else {
        pure assert Ix.fm[x][y].ks[k] == 0;
      }
      pure assume forall y: Node :: Ix.fm[x][y].ks[k] == 0;
      return null;
    }
  }

  var found, i := arr_find(x.keys, x.len, k);

  if (i < x.len && k == x.keys[i]) {  // arr_find finds upper bound, we want lower
    i := i + 1;
  }

  lemma_fm(x, Ix, k, i, x.len + 1);
  if (i == x.len && x.next != null) {
    if (lt(k, x.keys[i]))
      n := x.ptrs[i];
    else
      n := x.next;
  } else {
    pure assert i <= 0 || le(x.keys[i-1], k);
    n := x.ptrs[i];
  }
  // return n;
}


/** member */
procedure member(x: Node, k: K, Ix: Interface)
  returns (succ: Bool, res: Bool, Ix1: Interface)
  requires hrep(x, Ix) &*& intValid(Ix) == true
  requires Ix.inf[x].ks[k] == 1 && (forall y: Node :: Ix.fm[x][y].ks[k] == 0)
  ensures hrep(x, Ix)
  ensures intEq(Ix, Ix1) == true
  // decisiveOpSpec(x, Ix1, res, k):
  ensures succ ==> (Ix1.na.contents == Ix.na.contents && (res == k in Ix.na.contents))
{
  keyset_implies_leaf(x, Ix, k, 0);

  var idx: Int;
  res, idx := arr_find(x.keys, x.len, k);

  return true, res, Ix;
}


/** insert */
procedure insert(x: Node, k: K, Ix: Interface)
  returns (succ: Bool, res: Bool, Ix1: Interface)
  requires hrep(x, Ix) &*& intValid(Ix) == true
  requires Ix.inf[x].ks[k] == 1 && (forall y: Node :: Ix.fm[x][y].ks[k] == 0)
  ensures hrep(x, Ix1)
  ensures intEq(Ix, Ix1) == true
  // decisiveOpSpec(x, Ix1, res, k):
  ensures succ ==> (Ix1.na.contents == Ix.na.contents ++ {k} && (res == k !in Ix.na.contents))
{
  keyset_implies_leaf(x, Ix, k, 0);

  if (x.len < 2*B - 1) {

    ghost var m0 := x.keys.map;
    x.keys[x.len + 1] := x.keys[x.len];
    set_of_map_equal(m0, x.keys.map, 0, 0, x.len);
    ghost var m := x.keys.map; var old_len := x.len;
    var idx, new_len := arr_insert(x.keys, k, x.len);
    map_insert_content_set(m, x.keys.map, idx, k, x.len, new_len);
    x.len := new_len;

    // Define Ix1 to be Ix, but with k added to contents
    Ix1 := copy_interface(Ix, nd(Ix.na.contents ++ {k}, Ix.na.inr));
    lemma_int_valid_unfold(x, Ix);
    lemma_int_valid_fold(x, Ix1);

    return true, k !in Ix.na.contents, Ix1;
  } else {
    return false, false, Ix;
  }
}


/** delete */
procedure delete(x: Node, k: K, Ix: Interface)
  returns (succ: Bool, res: Bool, Ix1: Interface)
  requires hrep(x, Ix) &*& intValid(Ix) == true
  requires Ix.inf[x].ks[k] == 1 && (forall y: Node :: Ix.fm[x][y].ks[k] == 0)
  ensures hrep(x, Ix1)
  ensures intEq(Ix, Ix1) == true
  // decisiveOpSpec(x, Ix1, res, k):
  ensures succ ==> (Ix1.na.contents == Ix.na.contents -- {k} && (res == k in Ix.na.contents))
{
  keyset_implies_leaf(x, Ix, k, 0);

  ghost var m := x.keys.map;

  var new_len, idx := arr_delete(x.keys, k, x.len);

  map_delete_content_set(m, x.keys.map, x.len, new_len, idx, k);

  ghost var m1 := x.keys.map;
  x.keys[new_len] := x.keys[x.len];
  set_of_map_equal(m1, x.keys.map, 0, 0, new_len);

  x.len := new_len;

  // Define Ix1 to be Ix, but with k removed from contents
  Ix1 := copy_interface(Ix, nd(Ix.na.contents -- {k}, Ix.na.inr));
  lemma_int_valid_unfold(x, Ix);
  lemma_int_valid_fold(x, Ix1);

  return true, (k in Ix.na.contents), Ix1;
}

