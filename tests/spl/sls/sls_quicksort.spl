include "../include/slslist.spl";

procedure split(x: Node, y: Node, lb: Int, ub: Int) returns (pivot: Node)
  requires blseg(x, y, lb, ub) &*& x != y
  ensures  blseg(x, pivot, lb, pivot.data)
  ensures  blseg(pivot, y, pivot.data, ub) &*& pivot != y
  ensures  lb <= pivot.data <= ub
  ensures  Btwn(next, x, pivot, y)
{
  var curr := x.next;
  pivot := x;

  while (curr != y) 
    invariant blseg(x, pivot, lb, pivot.data)
    invariant blseg(pivot, curr, pivot.data, ub) &*& pivot != curr
    invariant blseg(curr, y, lb, ub)
    invariant lb <= pivot.data <= ub
    invariant Btwn(next, x, pivot, y)
    invariant Btwn(next, pivot, curr, y)
  {
    if (curr.data < pivot.data) {
      var tmp := curr.data;
      curr.data := pivot.next.data;
      pivot.next.data := pivot.data;
      pivot.data := tmp;
      pivot := pivot.next;
    }
    curr := curr.next;
  }
}

procedure quicksort(x: Node, y: Node, ghost lb: Int, ghost ub: Int)
  requires blseg(x, y, lb, ub)
  ensures  bslseg(x, y, lb, ub)
{
  if (x != y && x.next != y) {
    var pivot := split(x, y, lb, ub);
    quicksort(x, pivot, lb, pivot.data);
    quicksort(pivot.next, y, pivot.data, ub);
  }
}
