include "../include/dllist.spl";

procedure dl_insert(a: Node, b: Node, elt: Node) returns (c: Node, d: Node)
    requires dlseg(a, null, null, b) &*& acc(elt);
    ensures dlseg(c, null, null, d);
{
  if (a == null) {
    elt.prev := null;
    elt.next := null;
    return elt, elt;
  } else {
    c, d := a, b;
    var curr := c;
    var nondet: Bool;
    while (nondet && curr.next != null) 
      invariant acc(curr) -** dlseg(c, null, null, d);
    {
      curr := curr.next;
      havoc nondet;
    }
    elt.next := curr;
    var prv := curr.prev;
    curr.prev := elt;
    elt.prev := prv;
    if (prv != null) {
      prv.next := elt;
    } else {
      c := elt;
    }
    return c, d;
  }
}
