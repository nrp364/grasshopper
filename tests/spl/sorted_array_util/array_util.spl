options "-simplearrays"

include "../include/ordered_type.spl"

// project map segment m[i..j] to set of its elements
function set_of_map(m: Map<Int, K>, i: Int, j: Int)
  returns (res: Set<K>)
  requires i <= j
{
  i < j ? {m[i]} ++ set_of_map(m, i + 1, j) : {}
}

lemma extend_right(m: Map<Int, K>, i: Int, j: Int)
  requires i <= j
  pure ensures set_of_map(m, i, j) ++ {m[j]} == set_of_map(m, i, j + 1)
{
  if (i < j) {
    extend_right(m, i + 1, j);
  }
}

lemma in_set_of_map(m: Map<Int, K>, i: Int, j: Int)
  requires i <= j
  ensures forall k: Int :: i <= k < j ==> m[k] in set_of_map(m, i, j)
{
  if (i < j) {
    in_set_of_map(m, i + 1, j);
  }
}

lemma set_of_map_split(m: Map<Int, K>, i: Int, j: Int, k: Int)
  requires i <= j <= k
  ensures set_of_map(m, i, k) == set_of_map(m, i, j) ++ set_of_map(m, j, k)
{
  if (j < k) {
    extend_right(m, i, j);
    set_of_map_split(m, i, j + 1, k);
  }
}

lemma set_of_map_equal(m1: Map<Int, K>, m2: Map<Int, K>, i1: Int, i2: Int, len: Int)
  requires forall j: Int :: i1 <= j < i1 + len ==> m1[j] == m2[j + (i2 - i1)] @(matching m1[j] yields m2[j + (i2 - i1)])
  ensures set_of_map(m1, i1, i1 + len) == set_of_map(m2, i2, i2 + len)
{
  if (0 < len) {
    set_of_map_equal(m1, m2, i1 + 1, i2 + 1, len - 1);
  }
}

lemma frame_set_of_map(m: Map<Int, K>, i: Int, j: Int)
  requires i <= j
  ensures forall i1: Int, k: K :: i1 < i || j <= i1 ==> set_of_map(m, i, j) == set_of_map(m[i1 := k], i, j)
{
  if (i < j) {
    frame_set_of_map(m, i + 1, j);
  }
}

lemma not_in_sorted_seg(m: Map<Int, K>, i: Int, j: Int, k: K, implicit ghost C: Set<K>)
  requires sorted_map_seg(m, i, j) && C == set_of_map(m, i, j)
  ensures i >= j || lt(k, m[i]) || lt(m[j-1], k) ==> k !in C
{
  if (i >= j) return;
  
  if (lt(m[j - 1], k)) {
    extend_right(m, i, j - 1);
    not_in_sorted_seg(m, i, j - 1, k);
    return;
  }

  if (lt(k, m[i])) {
    not_in_sorted_seg(m, i + 1, j, k);    
    return;
  }
  
}

function map_shift(m: Map<Int, K>, src: Int, dst: Int, len: Int)
  returns (res: Map<Int, K>)
  requires 0 <= len
{
  { i: Int :: i < dst || dst + len <= i ? m[i] : m[src + (i - dst)]
    @(matching res[i] yields m[src + (i - dst)])
    @(matching res[i] yields m[i])
  }
}

function map_copy(m1: Map<Int, K>, m2: Map<Int, K>, src: Int, dst: Int, len: Int)
  returns (res: Map<Int, K>)
{
  { i: Int :: dst <= i < dst + len ? m2[src + (i - dst)] : m1[i]
    @(matching res[i] yields m2[src + (i - dst)])
    @(matching res[i] yields m1[i])
  }
}

function map_of_array(a: Array<K>) returns (res: Map<Int, K>)
  requires acc(a)
{
  { i: Int :: 0 <= i < a.length ? a[i] : bottom
    @(matching res[i] yields a[i])
    @(matching a[i] yields res[i])
  }
}

predicate sorted_map_seg(m: Map<Int, K>, i: Int, j: Int)
{
  forall i1: Int, i2: Int :: i <= i1 < i2 < j ==> lt(m[i1], m[i2])
}

predicate sorted_array_seg(a: Array<K>, i: Int, j: Int)
  requires acc(a)
{
  0 <= i && j <= a.length && sorted_map_seg(map_of_array(a), i, j)
}

function set_of_array(a: Array<K>, i: Int, j: Int) returns (res: Set<K>)
  requires acc(a) &*& 0 <= i && j <= a.length
{
  set_of_map(map_of_array(a), i, j)
}
  
predicate sorted_array_seg_with_content(a: Array<K>, i: Int, j: Int, C: Set<K>)
  requires acc(a)
{
  sorted_array_seg(a, i, j) && C == set_of_array(a, i, j)
}

predicate sorted_array_with_content(a: Array<K>, len: Int, m: Map<Int, K>)
{
  0 <= len &*&
    acc(a) &*& sorted_array_seg(a, 0, len) &*& m == map_of_array(a)
}


// Shift a[src..src+len] to a[dst..dst+len]
procedure arr_shift(a: Array<K>, src: Int, dst: Int, len: Int)
  requires acc(a)
  requires 0 <= src <= src + len <= a.length && 0 <= dst <= dst + len <= a.length
  ensures acc(a)
  ensures map_of_array(a) == map_shift(old(map_of_array(a)), src, dst, len)
{
  ghost var m := map_of_array(a);
  
  if (src < dst) {
    var i := len - 1;
    
    while (i >= 0) 
      invariant acc(a)
      invariant src < dst
      invariant 0 <= src <= src + len <= a.length
      invariant 0 <= dst <= dst + len <= a.length
      invariant -1 <= i < len
      invariant map_shift(m, src + i + 1, dst + i + 1, len - i - 1) == map_of_array(a)
    {
      ghost var m1 := map_of_array(a);
      var tmp := a[src + i];
      a[dst + i] := tmp;
      pure assert map_of_array(a) == m1[dst + i := tmp];
      i := i - 1;
    }
  } else if (src > dst) {
    var i := 0;
    while (i < len)
      invariant acc(a)
      invariant src > dst
      invariant 0 <= src <= src + len <= a.length
      invariant 0 <= dst <= dst + len <= a.length
      invariant 0 <= i <= len 
      invariant map_shift(m, src, dst, i) == map_of_array(a)
    {
      ghost var m1 := map_of_array(a);
      var tmp := a[src + i];
      a[dst + i] := a[src + i];
      pure assert map_of_array(a) == m1[dst + i := tmp];
      i := i + 1;
    }
  }
}

// Copy a[src..src+len] to b[dst..dst+len]
procedure arr_copy(a: Array<K>, b: Array<K>, src: Int, dst: Int, len: Int)
  requires acc(a) &*& acc(b)
  requires 0 <= src <= src + len <= a.length
  requires 0 <= dst <= dst + len <= b.length
  ensures acc(a) &*& acc(b)
  ensures map_of_array(a) == old(map_of_array(a))
  ensures map_of_array(b) == map_copy(old(map_of_array(b)), map_of_array(a), src, dst, len)
{
  ghost var mb := map_of_array(b);
  ghost var ma := map_of_array(a);
  var i := 0;
  
  while (i < len)
    invariant acc(a) &*& acc(b)
    invariant 0 <= i <= len
    invariant 0 <= src <= src + len <= a.length
    invariant 0 <= dst <= dst + len <= b.length
    invariant map_of_array(a) == ma
    invariant map_of_array(b) == map_copy(mb, ma, src, dst, i)
  {
    ghost var m1 := map_of_array(b);
    var tmp := a[src + i];
    b[dst + i] := tmp;
    pure assert map_of_array(b) == m1[dst + i := tmp];
    i := i + 1;
  }
}

function map_find(m: Map<Int, K>, i: Int, j: Int, k: K) returns (idx: Int)
  requires i <= j
  requires sorted_map_seg(m, i, j)
  ensures i <= idx <= j
  ensures m[idx] == k || idx == j || lt(k, m[idx])
  ensures i < idx ==> lt(m[idx - 1], k)
{
  i < j && lt(m[i], k) ? map_find(m, i + 1, j, k) : i
}

lemma map_find_in_set(m: Map<Int, K>, i: Int, j: Int, k: K)
  requires i <= j
  requires sorted_map_seg(m, i, j)
  ensures k in set_of_map(m, i, j) ==> map_find(m, i, j, k) < j && m[map_find(m, i, j, k)] == k
  ensures k !in set_of_map(m, i, j) ==> map_find(m, i, j, k) == j || lt(k, m[map_find(m, i, j, k)])
{
  var idx := map_find(m, i, j, k);
  map_find_content_set(m, i, j, idx, k);
  set_of_map_split(m, i, idx, j);
}

lemma map_find_content_set(m: Map<Int, K>, i: Int, j: Int, idx: Int, k: K)
  requires i <= j
  requires sorted_map_seg(m, i, j)
  requires idx == map_find(m, i, j, k)
  ensures k !in set_of_map(m, i, idx)
  ensures k !in set_of_map(m, idx + 1, j)
{
  // prove k !in set_of_map(a, 0, idx)
  not_in_sorted_seg(m, i, idx, k);
  // prove: k !in set_of_map(a, idx + 1, len)
  not_in_sorted_seg(m, idx + 1, j, k);
}

// Find key `k` in sorted array segment `a[0..len]` using binary search
procedure arr_find(a: Array<K>, len: Int, k: K, implicit ghost m: Map<Int, K>)
  returns (found: Bool, idx: Int)
  requires sorted_array_with_content(a, len, m)
  requires 0 <= len <= a.length
  ensures sorted_array_with_content(a, len, m)
  // what we actually care about
  ensures idx == map_find(m, 0, len, k)
  ensures found == (k in set_of_map(m, 0, len))
{
  var lo := 0;
  var hi := len;
  
  while (hi != lo)
    invariant sorted_array_with_content(a, len, m)
    // what we actually care about
    invariant 0 <= lo <= hi <= len <= a.length
    invariant hi == len || m[lo] == k || lt(k, m[hi])
    invariant 0 < lo ==> lt(m[lo - 1], k)
    invariant hi < len - 1 ==> lt(k, m[hi + 1])
  {
    var mid := (hi + lo) / 2;
    var cmp: Int;
    cmp := compare(k, a[mid]);
    if (cmp < 0) {
      hi := mid;      // look in first half
    } else if (cmp > 0) {
      lo := mid + 1;    // look in second half
    } else {
      // found it
      hi := mid;
      lo := mid;
    }
  }
  
  idx := lo;
  
  if (idx == len || lt(k, a[lo])) {
    found := false;
  } else {
    found := true;
  }

  map_find_in_set(m, 0, len, k);
}

lemma map_insert_content_set(m: Map<Int, K>, m1: Map<Int, K>, idx: Int, k: K, len: Int, new_len: Int)
  requires sorted_map_seg(m, 0, len)
  requires idx == map_find(m, 0, len, k)
  requires k in set_of_map(m, 0, len) ==> new_len == len && m1 == m
  requires k !in set_of_map(m, 0, len) ==> new_len == len + 1 && m1 == map_shift(m, idx, idx + 1, len - idx)[idx := k]
  ensures set_of_map(m1, 0, new_len) == set_of_map(m, 0, len) ++ {k}
{
  if (m[idx] == k && idx < len) {
    in_set_of_map(m, 0, len);
    return;
  }
  
  var ms := map_shift(m, idx, idx + 1, len - idx);

  // prove set_of_map(m, 0, len) == set_of_map(ms, 0, idx) ++ set_of_array(ms, idx + 1, len + 1);
  set_of_map_split(m, 0, idx, len);
  set_of_map_equal(m, ms, 0, 0, idx);
  set_of_map_equal(m, ms, idx, idx + 1, len - idx);

  // prove set_of_map(m1, 0, new_len) == set_of_array(ms, 0, idx) ++ {k} ++ set_of_map(ms, idx + 1, len + 1)
  frame_set_of_map(ms, 0, idx);
  frame_set_of_map(ms, idx + 1, len + 1);  
  set_of_map_split(m1, 0, idx, len + 1);
}

// Given a sorted array segment `a[0..len]`,
// insert `k` into `a[0..len+1]` while preserving sortedness.
// If `k` is already contained in `a[0..len]`, then do not modify `a`.
procedure arr_insert(a: Array<K>, k: K, len: Int, implicit ghost m: Map<Int, K>)
  returns (idx: Int, new_len: Int, implicit ghost m1: Map<Int, K>)
  requires sorted_array_with_content(a, len, m)
  requires len < a.length
  ensures sorted_array_with_content(a, new_len, m1)
  ensures idx == map_find(m, 0, len, k)
  ensures k in set_of_map(m, 0, len) ==> new_len == len && m1 == m
  ensures k !in set_of_map(m, 0, len) ==> new_len == len + 1 && m1 == map_shift(m, idx, idx + 1, len - idx)[idx := k]
{
  // find position for insertion
  var i: Int, found: Bool;
  found, i := arr_find(a, len, k);

  map_find_in_set(m, 0, len, k);
  
  // k already in C?
  if (found) return i, len;
  
  arr_shift(a, i, i + 1, len - i);

  ghost var ms := map_of_array(a);
  
  a[i] := k;

  pure assert map_of_array(a) == ms[i := k];
 
  return i, len + 1;
}

lemma arr_delete_content_set(m: Map<Int, K>, m1: Map<Int, K>, len: Int, new_len: Int, idx: Int, k: K)
  requires sorted_map_seg(m, 0, len)
  requires idx == map_find(m, 0, len, k)
  requires k !in set_of_map(m, 0, len) ==> new_len == len && m1 == m
  requires k in set_of_map(m, 0, len) ==> new_len == len - 1 && m1 == map_shift(m, idx + 1, idx, len - (idx + 1))
  ensures set_of_map(m1, 0, new_len) == set_of_map(m, 0, len) -- {k}
{
  map_find_content_set(m, 0, len, idx, k);
  set_of_map_split(m, 0, idx, len);
    
  if (idx == len || m[idx] != k) {
    return;
  }
  
  // prove: set_of_map(m, 0, len) -- {k} == set_of_map(m1, 0, idx - 1) ++ set_of_map(m1, idx - 1, len - 1)
  set_of_map_equal(m, m1, 0, 0, idx);
  set_of_map_equal(m, m1, idx + 1, idx, len - (idx + 1));

  not_in_sorted_seg(m, 0, idx - 1, k);
  not_in_sorted_seg(m, idx + 1, len, k);
  
  set_of_map_split(m1, 0, idx, len - 1);
}

// Given a sorted array segment `a[0..len]`,
// delete `k` from the segment while preserving sortedness.
// If `k` is already contained in `a[0..len]`, then do not modify `a`.
procedure arr_delete(a: Array<K>, k: K, len: Int, implicit ghost m: Map<Int, K>)
  returns (new_len: Int, idx: Int, implicit ghost m1: Map<Int, K>)
  requires sorted_array_with_content(a, len, m)
  ensures sorted_array_with_content(a, new_len, m1)
  ensures idx == map_find(m, 0, len, k)
  ensures k !in set_of_map(m, 0, len) ==> new_len == len && m1 == m
  ensures k in set_of_map(m, 0, len) ==> new_len == len - 1 && m1 == map_shift(m, idx + 1, idx, len - (idx + 1))
{
  // find position for insertion
  var found: Bool;
  found, idx := arr_find(a, len, k);

  map_find_in_set(m, 0, len, k);
  
  // k !in C?
  if (!found) {
    return len, idx;
  }
  
  // shift array entries a[i+1..len] by 1 entry to the left 
  //ghost var m := map_of_array(a);
  arr_shift(a, idx + 1, idx, len - (idx + 1));
  
  return len - 1, idx;
}
